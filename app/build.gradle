plugins {
    id 'com.android.application'
}
/**
 * 主Model App —> build.gradle
 * N个副Model -> build.gradle
 *
 * 1.统一性
 * 2.便于管理
 * 3.版本管理
 *
 * Gradle构建的三个性能指标：
 * - 全量编译：全部编译 - Open Project
 * - 代码增量编译：修改了Java/Kotlin下面的代码的时候编译
 * - 资源增量编译：修改了res下面的资源文件的时候编译
 *
 * 发布App到应用市场：多渠道打包/全部打包
 * 调试：不需要这么多，只需要DEBUG
 */
android {
    //开启viewBinding
    buildFeatures {
        viewBinding = true
    }

    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    //默认配置
    defaultConfig {
        applicationId rootProject.ext.android["applicationId"]
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        // All flavors must now belong to a named flavor dimension.
        // Android Studio 3.0 之后 多渠道需要配置方向
        flavorDimensions "default"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //调试 保留资源 传一个数组，发布时则要去掉
        //resConfigs("zh","xxhdpi")

        ndk {
            //"armeabi", "x86","x86_64"
            //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            //目前主流版本（Google Play上架要求app必须适配arm64-v8a）
            //abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    //签名配置 要先出现，再可以引用
    signingConfigs {
        release {
            keyAlias 'baseapp'
            keyPassword 'android'
            storeFile file('/src/main/jks/baseapp.jks')
            storePassword 'swiftpass'
        }
    }

    //打包配置
    buildTypes {
        debug {
            //自定义Gradle常量
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "LOG_TAG", "\"BaseAPP\""
            buildConfigField "String", "SP_NAME", "\"Config\""
        }
        release {
            //好处：可以区分debug和release
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "LOG_TAG", "\"BaseAPP\""
            buildConfigField "String", "SP_NAME", "\"Config\""
            //shrinkResources为删除无用资源，minifyEnabled为混淆，删除无用代码，但两者是要配合起来用的
            //只有当两者都为true的时候才会起到真正的删除无效代码和无引用资源的目的
            //清理无用资源
            shrinkResources false
            //是否启动ZipAlign压缩
            zipAlignEnabled true
            //是否混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release
        }
    }

    //渠道配置 All flavors must now belong to a named flavor dimension.
//    productFlavors {
//        xiaomi {
//            manifestPlaceholders = [APP_CHANNEL_VALUE: "xiaomi"]
//        }
//        baidu {
//            manifestPlaceholders = [APP_CHANNEL_VALUE: "baidu"]
//        }
//        wandoujia {
//            manifestPlaceholders = [APP_CHANNEL_VALUE: "wandoujia"]
//        }
//        yingyongbao {
//            manifestPlaceholders = [APP_CHANNEL_VALUE: "yingyongbao"]
//        }
//        //....
//    }


    //可以解决：java.lang.BootstrapMethodError: Exception from call site #8 bootstrap method
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    //v7
    implementation rootProject.ext.dependencies["appcompat"]
    //
    implementation rootProject.ext.dependencies["material"]
    implementation rootProject.ext.dependencies["constraintlayout"]
    //图片加载
    implementation rootProject.ext.dependencies["glide"]
    //EventBus
    implementation rootProject.ext.dependencies["eventbus"]
    //PhotoView 需要添加 maven { url "https://www.jitpack.io" }
    implementation rootProject.ext.dependencies["photoview"]
    //二维码zxingLibrary
    implementation rootProject.ext.dependencies["zxing"]
    //
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}